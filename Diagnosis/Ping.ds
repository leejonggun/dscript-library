// written by Joseph

import("dshell");
import("konoha.json");
import("cstyle");

const DCaseContext = Json;

String host = "";

boolean Ping(DCaseContext ctx) {
	host = ctx.getString("hostname");
	String lookup = "";

//check whether network cable is connected or not
	int default_gateway = $(ip route show).search("default");
	if (default_gateway == -1) {
		System.p("Network is disconnected. Please connect to the network.");
		return false;
	};

	ping "${host}" -c 2 > /dev/null;
	System.p($?);
	if ($? == 0) {//Success
		System.p("Success:Network path to the server is connected");
		return true;

	} else if ($? == 1) {//ping is unreachable
		String res = $(ping "${host}" -c 2);
		System.p(res);

//		if (res.search("Request timed out") != -1) {
		if (res.search("100% packet loss") != -1) {//Host is found, but the host drops the packet.
			lookup = $(minikonoha Nslookup.ds "${host}");
			if (lookup.search("true") > 0) {
				System.p("ExternalFault:Server doesn't respond. The server doesn't allow ping.");
			}
			System.p("SystemFault:Name Resolution is failed.");
			return false;
		} else if (res.search("Destination Host Unreachable") != -1) {//Network involves the host is found, but the host is not found.
			System.p("ExternalFault:Host is not found.");
			return false;
		} else if (res.search("Destination Net Unreachable") != -1) {//Network is not found.
			System.p("SystemFault:Dafault gateway setting is wrong.");
			String default_gateway = $(ip route show | grep "default");
			System.p(default_gateway);
			return false;
		} else if (res.search("Operation not permitted") != -1) {//firewall doesn't allow ping.
			sudo iptables -L | grep "DROP";
			System.p("SystemFault:Your firewall doesn't allow ping.");
			return false;
		} else if (res.search("Time to live exceeded") != -1) {//TTL is set too little
			System.p("ExternalFault:It's too far to the server.");
			return false;
		}
	} else if ($? == 2) {//Errors. can't do ping
		System.p(host);
		lookup = $(minikonoha Nslookup.ds "${host}");
		System.p(lookup);
		if (lookup.search("true") > 0) {
			System.p("SystemFault:Network is unreachable.");
			return false;
		}
		System.p("nslookup failed");
		System.p("SystemFault:Hostname is not found. The host is not on Nameserver");
		return false;
	}
	System.p("Ping.ds is not working");
	return ($? == 0);
}

void main() {
	DCaseContext ctx = new DCaseContext();
	ctx.setString("hostname", SCRIPT_ARGV.get(1));
	System.p(Ping(ctx));
}

main();
