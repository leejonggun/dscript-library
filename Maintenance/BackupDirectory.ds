// written by chenji

import("dshell");
import("konoha.json");

const DCaseContext = Json;

@Public boolean BackupDirectory(DCaseContext ctx) {
	String sourcedir = ctx.getString("SourceDirectory");
	String destdir = ctx.getString("DestinationDirectory");
	String ipaddr = ctx.getString("DestinationIp");
	String user = ctx.getString("DestinationUser");
	System.p("backup from " + sourcedir + " to " + destdir);
	test -d "${destdir}/backup.0";
	if($? == 0) {
		rm -rf "${destdir}/backup.3";
		mv "${destdir}/backup.2" "${destdir}/backup.3";
		mv "${destdir}/backup.1" "${destdir}/backup.2";
		mv "${destdir}/backup.0" "${destdir}/backup.1";
	}
	rsync -v --archive --delete -F --link-dest=../backup.1 "${user}@${ipaddr}:${sourcedir}" "${destdir}/backup.pre";
	if($? == 0) {
		mv "${destdir}/backup.pre" "${destdir}/backup.0";
		return true;
	}
	return false;
}

void main() {
	DCaseContext ctx = new Json();
	ctx.setString("SourceDirectory", "/path/to/sourcedir");
	ctx.setString("DestinationDirectory", "/path/to/destdir");
	ctx.setString("DestinationIp", "ipaddr");
	ctx.setString("DestinationUser", "user");
	System.p(BackupDirectory(ctx));
}

main();
